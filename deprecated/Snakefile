from os.path import join as j

import pandas as pd

###############################################################################
# Raw datasets
###############################################################################

# Data file from Our World in Data. Directly from ECDC
OWID_DIR = 'data_sources/our_world_in_data'
OWID_WHO_TS = j(OWID_DIR, 'owid_who_ts.csv')
OWID_ECDC_TS = j(OWID_DIR, 'owid_ecdc_ts.csv')

# Data feed from Tableau. Raw data is from JHU CSSE GitHub. currently not used.
TABLEAU_TS = 'data_sources/tableau/tableau_ts.csv'

# Population data from Worldbank
WB_RAW = "data_sources/worldbank_population_data/wb_raw.csv"

# country code data from wikipedia
WP_CNTRY_RAW = 'data_sources/wikipedia/ISO3166/iso3166_country_code.csv'
US_STATE_CODE = 'data_sources/wikipedia/ISO3166/usa_state_code.csv'

# NYT data
NYT_STATE_TS_RAW = 'data_sources/nyt/covid-19-data/us-states.csv'

###############################################################################
# Manually curated datasets
###############################################################################

# US confirmed & death time series data from Wikipedia
WP_CASE_DIR = 'data_sources/wikipedia/cases'
WP_TS = j(WP_CASE_DIR, 'country-level/{country}_ts.csv')
WP_COUNTRIES = pd.read_csv(
    j(WP_CASE_DIR, 'country_case_wp_pages.csv'))['country_code'].to_list()

WB_ADDED = 'curation_data/country/extra_country_metadata.csv'
EXTRA_CNTRY_NAME_CODE = 'curation_data/country/extra_country_name_code.csv'

###############################################################################
# Final outputs
###############################################################################

OUT_META_DIR = 'output/metadata'
CASE_DATA_DIR = 'output/cases'

# Location data pulled from tableau/jhu dataset.
COORDINATES = 'output/location/coordinates.csv'

# Country name and code conversion table
CNTRY_NAME_CODE_TABLE = j(OUT_META_DIR, 'country/country_name_code.csv')
CNTRY_CODE_NAME_TABLE = j(OUT_META_DIR, 'country/country_code_name.csv')

# Country-level metadata
CNTRY_META = 'output/metadata/country/country_metadata.csv'

# json file for the visualization: http://yyahn.com/covid19
CNTRY_STAT_JSON = 'output/cntry_stat.json'
CNTRY_STAT_JSON_FROM_OWID = 'output/cntry_stat_owid.json'
CNTRY_STAT_JSON_WHO_WP = 'output/cntry_stat_who_wp.json'
CNTRY_STAT_JSON_ECDC_WP = 'output/cntry_stat_ecdc_wp.json'
USA_STAT_JSON_FROM_NYT = 'output/us_state_nyt.json'

# WHO case data csv
WHO_CASE_DATA = j(CASE_DATA_DIR, 'cases_WHO.csv')
WHO_WP_CASE_DATA = j(CASE_DATA_DIR, 'cases_WHO_WP.csv')

# ECDC case data csv
ECDC_CASE_DATA = j(CASE_DATA_DIR, 'cases_ECDC.csv')
ECDC_WP_CASE_DATA = j(CASE_DATA_DIR, 'cases_ECDC_WP.csv')

# USA state data from NYT
NYT_STATE_TS = 'output/cases/cases_us_states_nyt.csv'

###############################################################################
# Workflows
###############################################################################

rule all:
    input:
        CNTRY_STAT_JSON_FROM_OWID,
        CNTRY_STAT_JSON_ECDC_WP,
        COORDINATES,
        ECDC_WP_CASE_DATA,
        NYT_STATE_TS,
        USA_STAT_JSON_FROM_NYT,

rule nyt_state_ts:
    input: NYT_STATE_TS_RAW, US_STATE_CODE
    output: NYT_STATE_TS
    shell: "python scripts/nyt_state_data.py {input} {output}"

rule extract_country_code_name_table:
    input: CNTRY_NAME_CODE_TABLE
    output: CNTRY_CODE_NAME_TABLE
    script: "scripts/extract_country_code_name_table.py"

rule extract_country_name_code_table:
    input: WP_CNTRY_RAW, WB_RAW, WB_ADDED, EXTRA_CNTRY_NAME_CODE
    output: CNTRY_NAME_CODE_TABLE
    script: "scripts/extract_country_name_code_table.py"

rule extract_ecdc_wp_case_data:
    input: ECDC_CASE_DATA, expand(WP_TS, country=WP_COUNTRIES)
    output: ECDC_WP_CASE_DATA
    script: "scripts/extract_ecdc_wp_case_data.py"

rule prepare_viz_data:
    input: ECDC_WP_CASE_DATA, CNTRY_META, CNTRY_CODE_NAME_TABLE
    output: CNTRY_STAT_JSON_ECDC_WP
    script: "scripts/prepare_viz_data_owid.py"

rule prepare_viz_data_from_owid:
    input: ECDC_CASE_DATA, CNTRY_META, CNTRY_CODE_NAME_TABLE
    output: CNTRY_STAT_JSON_FROM_OWID
    script: "scripts/prepare_viz_data_owid.py"

rule prepare_us_states_viz_data:
    input: NYT_STATE_TS
    output: USA_STAT_JSON_FROM_NYT
    shell: "python scripts/prepare_state_level_viz_data.py {input} {output}"

rule extract_country_metadata:
    input: WB_RAW, WB_ADDED
    output: CNTRY_META
    script: "scripts/extract_country_metadata.py"

rule extract_owid_case_data:
    input: OWID_ECDC_TS, CNTRY_NAME_CODE_TABLE, CNTRY_CODE_NAME_TABLE
    output: ECDC_CASE_DATA
    script: "scripts/owid_ecdc_case_data.py"

###############################################################################
# Deprecated workflows
###############################################################################

# Deprecated: OWID is not using WHO anymore.
# rule extract_who_case_data:
    # input: OWID_WHO_TS, CNTRY_NAME_CODE_TABLE, CNTRY_CODE_NAME_TABLE
    # output: WHO_CASE_DATA
    # script: "scripts/owid_who_case_data.py"

# rule prepare_viz_data:
    # input: WHO_WP_CASE_DATA, CNTRY_META, CNTRY_CODE_NAME_TABLE
    # output: CNTRY_STAT_JSON_WHO_WP
    # script: "scripts/prepare_viz_data_owid.py"

# Deprecated: Tableau data relies on JHU github, which has many issues.
# rule extract_coordinates:
    # input: TABLEAU_TS
    # output: COORDINATES
    # script: "scripts/extract_coordinates.py"
